<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <meta name="description" content="SARL is a general-purpose agent-oriented language.
SARL aims at providing the fundamental abstractions for dealing with concurrency, distribution, interaction, decentralization, reactivity, autonomy and dynamic reconfiguration. These high-level features are now considered as the major requirements for an easy and practical implementation of modern complex software applications. We are convinced that the agent-oriented paradigm holds the keys to effectively meet this challenge.
Considering the variety of existing approaches and meta-models in the field of agent-oriented engineering and more generally multi-agent systems, our approach remains as generic as possible and highly extensible to easily integrate new concepts and features. The language is platform- and architecture-independent.
">
    <meta name="author" content="SARL Team">
    <link rel="icon" href="../../../images/favicon.ico" />
    <link rel="shortcut icon" href="../../../images/favicon.ico">

    <title>Introduction to Lists with SARL - Answers</title>

    <link rel="stylesheet" href="../../../css/bootstrap.min.css">
    <link rel="stylesheet" href="../../../css/font-awesome.min.css">
    <link rel="stylesheet" href="../../../css/pygments.css">
    <link rel="stylesheet" href="../../../css/custom.css">

    <!-- HTML5 shim and Respond.js IE8 support of HTML5 elements and media queries -->
    <!--[if lt IE 9]>
      <script src="https://oss.maxcdn.com/libs/html5shiv/3.7.0/html5shiv.js"></script>
      <script src="https://oss.maxcdn.com/libs/respond.js/1.4.2/respond.min.js"></script>
    <![endif]-->
  </head>
  <body>

    

<!-- Fixed navbar -->
<div class="navbar navbar-default navbar-fixed-top" role="navigation">
  <div class="container">
    <div class="navbar-header">
      <button type="button" class="navbar-toggle" data-toggle="collapse" data-target=".navbar-collapse">
        <span class="sr-only">Toggle navigation</span>
        <span class="icon-bar"></span>
        <span class="icon-bar"></span>
        <span class="icon-bar"></span>
      </button>
      <a class="navbar-brand" href="http://www.sarl.io">SARL</a>
    </div>
    <div class="navbar-collapse collapse">
      <ul class="nav navbar-nav">
        <li><a href="../../../index.html">Home</a></li>
        <li><a href="../../../download/index.html">Download</a></li>
        <li><a href="../../../library/index.html">Library</a></li>
        <li><a href="../../../docs/index.html">Documentation</a></li>
        <li><a href="../../../community/index.html">Community</a></li>
        <li><a href="../../../runtime/index.html">Run-time</a></li>
        <li><a href="../../../news/index.html">News &amp; Events</a></li>
        <li><a href="../../../publications/index.html">Publications &amp; Presentations</a></li>
        <li><a href="../../../about/index.html">About</a></li>
        <li><a href="../../../search/index.html"><i class="fa fa-search"></i></a></li>
      </ul>
    </div><!--/.nav-collapse -->
  </div>
</div>

    <div class="container">
		
		<a href="https://github.com/sarl/sarl" target="_blank"><img
			style="position: absolute; top: 40px; right: 0; border: 0;"
			src="https://github-camo.global.ssl.fastly.net/365986a132ccd6a44c23a9169022c0b5c890c387/68747470733a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f7265645f6161303030302e706e67" 
			alt="Fork me on GitHub"
			data-canonical-src="https://s3.amazonaws.com/github/ribbons/forkme_right_red_aa0000.png"></a>
		
      <h1 id="introduction-to-lists-with-sarl---answers">Introduction to Lists with SARL - Answers</h1>

<ul class="page_outline" id="page_outline">

<li><a href="#1-exercise-1">1. Exercise 1</a></li>
<li><a href="#2-exercise-2">2. Exercise 2</a></li>
<li><a href="#3-exercise-3">3. Exercise 3</a></li>
<li><a href="#4-exercise-4">4. Exercise 4</a></li>
<li><a href="#5-exercise-5">5. Exercise 5</a></li>
<li><a href="#6-exercise-6">6. Exercise 6</a></li>
<li><a href="#7-exercise-7">7. Exercise 7</a></li>
<li><a href="#8-exercise-8">8. Exercise 8</a></li>
<li><a href="#9-exercise-9">9. Exercise 9</a></li>
<li><a href="#10-exercise-10">10. Exercise 10</a></li>
<li><a href="#11-exercise-11">11. Exercise 11</a></li>
<li><a href="#12-exercise-12">12. Exercise 12</a></li>
<li><a href="#13-exercise-13">13. Exercise 13</a></li>
<li><a href="#14-exercise-14">14. Exercise 14</a></li>
<li><a href="#15-exercise-15">15. Exercise 15</a></li>
<li><a href="#16-exercise-16">16. Exercise 16</a></li>
<li><a href="#17-exercise-17">17. Exercise 17</a></li>
<li><a href="#18-exercise-18">18. Exercise 18</a></li>
<li><a href="#19-exercise-19">19. Exercise 19</a></li>
<li><a href="#20-exercise-20">20. Exercise 20</a></li>
<li><a href="#21-exercise-21">21. Exercise 21</a></li>
<li><a href="#22-exercise-22">22. Exercise 22</a></li>
<li><a href="#23-exercise-23">23. Exercise 23</a></li>
<li><a href="#24-exercise-24">24. Exercise 24</a></li>
<li><a href="#25-exercise-25">25. Exercise 25</a></li>
<li><a href="#26-exercise-26">26. Exercise 26</a></li>
<li><a href="#27-exercise-27">27. Exercise 27</a></li>
<li><a href="#28-exercise-28">28. Exercise 28</a></li>
<li><a href="#29-exercise-29">29. Exercise 29</a></li>
<li><a href="#30-exercise-30">30. Exercise 30</a></li>
<li><a href="#31-exercise-31">31. Exercise 31</a></li>
<li><a href="#32-exercise-32">32. Exercise 32</a></li>
<li><a href="#33-exercise-33">33. Exercise 33</a></li>
<li><a href="#34-exercise-34">34. Exercise 34</a></li>
<li><a href="#35-exercise-35">35. Exercise 35</a></li>
<li><a href="#36-exercise-36">36. Exercise 36</a></li>
<li><a href="#37-exercise-37">37. Exercise 37</a></li>
<li><a href="#38-exercise-38">38. Exercise 38</a></li>
<li><a href="#39-exercise-39">39. Exercise 39</a></li>
<li><a href="#40-exercise-40">40. Exercise 40</a></li>
<li><a href="#41-exercise-41">41. Exercise 41</a></li>
<li><a href="#42-exercise-42">42. Exercise 42</a></li>
<li><a href="#43-exercise-43">43. Exercise 43</a></li>
<li><a href="#44-exercise-44">44. Exercise 44</a></li>
<li><a href="#45-exercise-45">45. Exercise 45</a></li>
<li><a href="#46-exercise-46">46. Exercise 46</a></li>
<li><a href="#47-exercise-47">47. Exercise 47</a></li>
<li><a href="#48-exercise-48">48. Exercise 48</a></li>
<li><a href="#49-exercise-49">49. Exercise 49</a></li>
<li><a href="#50-exercise-50">50. Exercise 50</a></li>
<li><a href="#51-exercise-51">51. Exercise 51</a></li>
<li><a href="#52-exercise-52">52. Exercise 52</a></li>
<li><a href="#53-exercise-53">53. Exercise 53</a></li>
<li><a href="#54-exercise-54">54. Exercise 54</a></li>
<li><a href="#55-exercise-55">55. Exercise 55</a></li>
<li><a href="#56-exercise-56">56. Exercise 56</a></li>
<li><a href="#57-exercise-57">57. Exercise 57</a></li>
<li><a href="#58-exercise-58">58. Exercise 58</a></li>
<li><a href="#59-exercise-59">59. Exercise 59</a></li>
<li><a href="#60-exercise-60">60. Exercise 60</a></li>
<li><a href="#61-legal-notice">61. Legal Notice</a></li>

</ul>

<h2 id="1-exercise-1">1. Exercise 1</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution1 {
	static def sum(list : List&lt;? extends Number&gt;) : Number {
		var s = 0.0
		for (e : list) {
			s += e
		}
		return s
	}
}
class Solution2 {
	static def sum(list : List&lt;? extends Number&gt;) : Number {
		list.reduce[accumulator, current | accumulator.doubleValue + current.doubleValue]
	}
}
</code></pre>

<h2 id="2-exercise-2">2. Exercise 2</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution1 {
	static def mul(list : List&lt;? extends Number&gt;) : Number {
		var s = 0.0
		for (e : list) {
			s *= e
		}
		return s
	}
}
class Solution2 {
	static def mul(list : List&lt;? extends Number&gt;) : Number {
		list.reduce[accumulator, current | accumulator.doubleValue * current.doubleValue]
	}
}
</code></pre>

<h2 id="3-exercise-3">3. Exercise 3</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution1 {
	static def maxValue(list : List&lt;? extends Number&gt;) : Number {
		var m : Number = null
		for (e : list) {
			if (m === null || m &lt; e) {
				m = e
			}
		}
		return m
	}
}
class Solution2 {
	static def maxValue(list : List&lt;? extends Number&gt;) : Number {
		list.max[a, b | a &lt;=&gt; b]
	}
}
</code></pre>

<h2 id="4-exercise-4">4. Exercise 4</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution1 {
	static var original = #['abc', 'xyz', 'aba', '1221']
	static def countStrings(list : List&lt;String&gt;) : int {
		var n = 0
		for (s : list) {
			if (s.length &gt;= 2 &amp;&amp; s.charAt(0) == s.charAt(s.length - 1)) {
				n++
			}
		}
		return n
	}
	static def main(args : String*) {
		println(countStrings(original))
	}
}
class Solution2 {
	static var original = #['abc', 'xyz', 'aba', '1221']
	static def countStrings(list : List&lt;String&gt;) : int {
		list.filter[it.length &gt;= 2 &amp;&amp; it.charAt(0) == it.charAt(it.length - 1)].size
	}
	static def main(args : String*) {
		println(countStrings(original))
	}
}
</code></pre>

<h2 id="5-exercise-5">5. Exercise 5</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution {
	static var original = #[#[2, 5], #[1, 2], #[4, 4], #[2, 3], #[2, 1]]
	static def main(args : String*) {
		var result = original.sortWith[a, b | 
			a.get(1) &lt;=&gt; b.get(1)
		]
		println(result)
	}
}
</code></pre>

<h2 id="6-exercise-6">6. Exercise 6</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution {
	static def remove_duplicates(list : List&lt;?&gt;) {
		var result = newArrayList
		for (e : list) {
			if (!result.contains(e)) {
				result += e
			}
		}
		return result
	}
}
</code></pre>

<h2 id="7-exercise-7">7. Exercise 7</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution {
	static def enquiry(list : List&lt;?&gt;) {
		list.size === 0
	}
}
</code></pre>

<h2 id="8-exercise-8">8. Exercise 8</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution {
	static def copy(list : List&lt;?&gt;) {
		var cp = newArrayList
		cp.addAll(list)
		return cp
	}
}
</code></pre>

<h2 id="9-exercise-9">9. Exercise 9</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution1 {
	static def words(list : List&lt;String&gt;, n : int) {
		var cp = newArrayList
		for (e : list) {
			if (e.length &gt; n) {
				cp += e
			}
		}
		return cp
	}
}
class Solution2 {
	static def words(list : List&lt;String&gt;, n : int) {
		list.filter[it.length &gt; n].toList
	}
}
</code></pre>

<h2 id="10-exercise-10">10. Exercise 10</h2>

<pre><code class="language-sarl">import java.util.List	
class Solution1 {
	static def common(list1 : List&lt;?&gt;, list2 : List&lt;?&gt;) {
		for (e : list1) {
			if (list2.contains(e)) {
				return true
			}
		}
		return false
	}
}
class Solution2 {
	static def words(list1 : List&lt;String&gt;, list2 : List&lt;?&gt;) {
		list1.exists[list2.contains(it)]
	}
}
</code></pre>

<h2 id="11-exercise-11">11. Exercise 11</h2>

<pre><code class="language-sarl">class Solution {
	static var original = #['Red', 'Green', 'White', 'Black', 'Pink', 'Yellow']
	static def main(args : String*) {
		original.remove(5)
		original.remove(4)
		original.remove(0)
		println(original)
	}
}
</code></pre>

<h2 id="12-exercise-12">12. Exercise 12</h2>

<pre><code class="language-sarl">class Solution1 {
	static def main(args : String*) {
		var result = newArrayList
		for (a : 1..3) {
			var dim2 = newArrayList
			for (b : 1..4) {
				var dim3 = newArrayList
				for (c : 1..6) {
					dim3 += '*'
				}
				dim2 += dim3
			}
			result += dim2
		}
		println(result)
	}
}
class Solution2 {
	static def main(args : String*) {
		var result = (1..3).map[
			(1..4).map[
				(1..6).map['*'].toList
			].toList
		].toList
		println(result)
	}
}
</code></pre>

<h2 id="13-exercise-13">13. Exercise 13</h2>

<pre><code class="language-sarl">import java.util.List
class Solution1 {
	static def remove_even(list : List&lt;Integer&gt;) : List&lt;Integer&gt; {
		var nlist = newArrayList
		for (e : list) {
			if ((e % 2) != 0) {
				nlist += e
			}
		}
		return nlist
	}
}
class Solution2 {
	static def remove_even(list : List&lt;Integer&gt;) : List&lt;Integer&gt; {
		list.filter[(it % 2) != 0].toList
	}
}
</code></pre>

<h2 id="14-exercise-14">14. Exercise 14</h2>

<pre><code class="language-sarl">import static extension java.util.Collections.shuffle
class Solution {
	static var original = #['Red', 'Green', 'White', 'Black', 'Pink', 'Yellow']
	static def main(args : String*) {
		original.shuffle
		println(original)
	}
}
</code></pre>

<h2 id="15-exercise-15">15. Exercise 15</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def swap(elements : Object[], index0 : int, index1 : int) : void {
		var tmp = elements.get(index0)
		elements.set(index0, elements.get(index1))
		elements.set(index1, tmp);
	}
	static def permuts(list : List&lt;Object&gt;) : List&lt;List&lt;Object&gt;&gt; {
		var permuts = newArrayList
		permuts.add(list.immutableCopy)
		if (list.size &gt; 1) {
			var elements = list.toArray
			var indexes = newIntArrayOfSize(list.size)
			var i = 0
			while (i &lt; list.size) {
			    if (indexes.get(i) &lt; i) {
			       swap(elements, if (i % 2 == 0) 0 else indexes.get(i), i)
					permuts.add(newArrayList(elements))
			       indexes.set(i, indexes.get(i) + 1);
			       i = 0
			    } else {
			       indexes.set(i, 0)
			       i++
			    }
			}
		}
		return permuts
	}
}
</code></pre>

<h2 id="16-exercise-16">16. Exercise 16</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def difference(a : List&lt;Object&gt;, b : List&lt;Object&gt;) : List&lt;Object&gt; {
		var diff = newArrayList
		for (element : a) {
			if (!b.contains(element)) {
				diff += element
			}
		}
		return diff
	}
}
</code></pre>

<h2 id="17-exercise-17">17. Exercise 17</h2>

<pre><code class="language-sarl">class Solution1 {
	static var original = #[5, 15, 35, 8, 98]
	static def main(args : String*) {
		var i = 0
		for (element : original) {
			println(i + " " + element)
			i++
		}
	}
}
class Solution2 {
	static var original = #[5, 15, 35, 8, 98]
	static def main(args : String*) {
		for (var i = 0; i &lt; original.size; i++) {
			println(i + " " + original.get(i))
		}
	}
}
class Solution3 {
	static var original = #[5, 15, 35, 8, 98]
	static def main(args : String*) {
		original.forEach [element, index |
			println(index + " " + element)
		]
	}
}
</code></pre>

<h2 id="18-exercise-18">18. Exercise 18</h2>

<pre><code class="language-sarl">import java.util.List
class Solution1 {
	static var original : List&lt;Character&gt; = #['h', 'e', 'l', 'l', 'o']
	static def main(args : String*) {
		var str = ""
		for (c : original) {
			str += c
		}
		println(str)
	}
}
class Solution2 {
	static var original : List&lt;Character&gt; = #['h', 'e', 'l', 'l', 'o']
	static def main(args : String*) {
		var str_buffer = new StringBuilder
		for (c : original) {
			str_buffer.append(c)
		}
		var str = str_buffer.toString
		println(str)
	}
}
class Solution3 {
	static var original : List&lt;Character&gt; = #['h', 'e', 'l', 'l', 'o']
	static def main(args : String*) {
		var str = original.join("")
		println(str)
	}
}
</code></pre>

<h2 id="19-exercise-19">19. Exercise 19</h2>

<pre><code class="language-sarl">import java.util.List
class Solution1 {
	static var original : List&lt;Character&gt; = #['h', 'e', 'l', 'l', 'o']
	static def findIndexOf(list : List&lt;?&gt;, element : Object) : int {
		var i = 0
		for (elt : list) {
			if (elt == element) {
				return i
			}
			i++
		}
		return -1
	}
	static def main(args : String*) {
		println(original.findIndexOf('h'))
		println(original.findIndexOf('e'))
		println(original.findIndexOf('l'))
		println(original.findIndexOf('o'))
	}
}
class Solution2 {
	static var original : List&lt;Character&gt; = #['h', 'e', 'l', 'l', 'o']
	static def main(args : String*) {
		println(original.indexOf('h'))
		println(original.indexOf('e'))
		println(original.indexOf('l'))
		println(original.indexOf('o'))
	}
}
</code></pre>

<h2 id="20-exercise-20">20. Exercise 20</h2>

<pre><code class="language-sarl">import java.util.List
class Solution1 {
	static var original = #[#[4], #[0, 658, 4, 6], #[1, 2, 3]]
	static def flattenList(list : List&lt;List&lt;Integer&gt;&gt;) : List&lt;Integer&gt; {
		var output = newArrayList
		for (sublist : list) {
			for (element : sublist) {
				output += element
			}
		}
		return output
	}
	static def main(args : String*) {
		println(original.flattenList)
	}
}
class Solution2 {
	static var original = #[#[4], #[0, 658, 4, 6], #[1, 2, 3]]
	static def flattenList(list : List&lt;List&lt;Integer&gt;&gt;) : List&lt;Integer&gt; {
		var output = newArrayList
		for (sublist : list) {
			output += sublist
		}
		return output
	}
	static def main(args : String*) {
		println(original.flattenList)
	}
}
class Solution3 {
	static var original = #[#[4], #[0, 658, 4, 6], #[1, 2, 3]]
	static def main(args : String*) {
		println(original.flatten.toList)
	}
}
</code></pre>

<h2 id="21-exercise-21">21. Exercise 21</h2>

<pre><code class="language-sarl">class Solution1 {
	static var original1 = #[1, 45, 8, 6, 1]
	static var original2 = #[457, -1, 5]
	static def main(args : String*) {
		for (c : original2) {
			original1 += c
		}
		println(original1)
	}
}
class Solution2 {
	static var original1 = #[1, 45, 8, 6, 1]
	static var original2 = #[457, -1, 5]
	static def main(args : String*) {
		original1 += original2
		println(original1)
	}
}
</code></pre>

<h2 id="22-exercise-22">22. Exercise 22</h2>

<pre><code class="language-sarl">import java.util.Random
class Solution {
	static var original = #[1, 45, 8, 6, 1]
	static def main(args : String*) {
		var random = new Random
		var element = original.get(random.nextInt(original.size))
		println(element)
	}
}
</code></pre>

<h2 id="23-exercise-23">23. Exercise 23</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var list1 = #[10, 10, 0, 0, 10]
	static var list2 = #[10, 10, 10, 0, 0]
	static def circularly_identical(list1 : List&lt;Integer&gt;, list2 : List&lt;Integer&gt;) : boolean {
		// doubling list
		var list3 = newArrayList(list1)
	    list3.addAll(list1)
		// traversal in twice of list1
		for (x : 0..&lt;list1.size) {
			var z = 0
			// check if list2 == list1 circularly
	        for (y : x..&lt;(x + list1.size)) {
	            if (list2.get(z) == list3.get(y)) {
	            	z += 1
	            } else {
	            	break
	            }
	        }
	             
			// if all n elements are same circularly
			if (z == list1.size) {
				return true
			}
		}
	    return false
	}
  
	static def main(args : String*) {
		println(circularly_identical(list1, list2))
	}
}
</code></pre>

<h2 id="24-exercise-24">24. Exercise 24</h2>

<pre><code class="language-sarl">class Solution1 {
	static var original = #[1, 45, -45, 2, 987]
	static def main(args : String*) {
		var smaller : Integer = null
		for (c : original) {
			if (smaller === null || c &lt; smaller) {
				smaller = c
			}
		}
		var secondSmaller : Integer = null
		for (c : original) {
			if (c &gt; smaller &amp;&amp; (smaller === null || c &lt; secondSmaller)) {
				secondSmaller = c
			}
		}
		println(secondSmaller)
	}
}
class Solution2 {
	static var original = #[1, 45, -45, 2, 987]
	static def main(args : String*) {
		var smaller = original.min
		var secondSmaller = original.filter[it &gt; smaller].min
		println(secondSmaller)
	}
}
</code></pre>

<h2 id="25-exercise-25">25. Exercise 25</h2>

<pre><code class="language-sarl">import java.util.List
class Solution1 {
	static var original = #[1, 45, -45, 1, 2, 987, 45]
	static def get_uniq(list : List&lt;Integer&gt;) : List&lt;Integer&gt; {
		var uniq = newArrayList
		for (c : list) {
			if (!uniq.contains(c)) {
				uniq += c
			}
		}
		return uniq
	}
	static def main(args : String*) {
		println(get_uniq(original))
	}
}
class Solution2 {
	static var original = #[1, 45, -45, 1, 2, 987, 45]
	static def get_uniq(list : List&lt;Integer&gt;) : List&lt;Integer&gt; {
		var uniq = newTreeSet(null)
		uniq.addAll(list)
		return uniq.toList
	}
	static def main(args : String*) {
		println(get_uniq(original))
	}
}
</code></pre>

<h2 id="26-exercise-26">26. Exercise 26</h2>

<pre><code class="language-sarl">import java.util.List
import java.util.Map
class Solution {
	static var original = #[1, 45, -45, 1, 2, 987, 45]
	static def frequency(list : List&lt;T&gt;) : Map&lt;T, Integer&gt; with T {
		var map = newHashMap
		for (c : list) {
			var n = map.getOrDefault(c, 0)
			map.put(c, n + 1)
		}
		return map
	}
	static def main(args : String*) {
		println(frequency(original))
	}
}
</code></pre>

<h2 id="27-exercise-27">27. Exercise 27</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[1, 45, -45, 1, 2, 987, 45]
	static def is_sublist(list : List&lt;Integer&gt;, search_for : List&lt;Integer&gt;) : boolean {
		if (search_for.isEmpty) {
			return true
		}
		if (search_for.size &gt; list.size) {
			return false
		}
		for (i : 0..&lt;list.size) {
			if (list.get(i) == search_for.get(0)) {
				var n = 1
				while ((n &lt; search_for.size) &amp;&amp; (list.get(i + n) == search_for.get(n))) {
					n++
				}
				if (n == search_for.size) {
					return true
				}
			}
		}
		return false
	}
	static def main(args : String*) {
		println(is_sublist(original, #[1, 2, 987]))
		println(is_sublist(original, #[1, 987, 2]))
	}
}
</code></pre>

<h2 id="28-exercise-28">28. Exercise 28</h2>

<pre><code class="language-sarl">import java.util.BitSet
import java.util.List
class Solution1 {
	static def sieve_of_eratosthenes(num : int) {
		var prime = newArrayList
		for (i : 0..num) {
			prime += true
		}
		var p = 2
		while (p * p &lt;= num) {
			// If prime[p] is not changed, then it is a prime
			if (prime.get(p)) {
				// Updating all multiples of p
				for (var i = p * p; i &lt;= num; i += p) {
					prime.set(i, false)
				}
			}
			p += 1
		}
		// Print all prime numbers
		for (p2 : 2..num) {
			if (prime.get(p2)) {
    			println(p2)
    		}
    	}
	}
	static def main(args : String*) {
		sieve_of_eratosthenes(10)
		sieve_of_eratosthenes(20)
	}
}
class Solution2 {
	static def sieve_of_eratosthenes(num : int) {
		var prime = new BitSet(num)
		var p = 2
		while (p * p &lt;= num) {
			// If prime[p] is not changed, then it is a prime
			if (!prime.get(p)) {
				// Updating all multiples of p
				for (var i = p * p; i &lt;= num; i += p) {
					prime.set(i)
				}
			}
			p += 1
		}
		// Print all prime numbers
		for (i : 2..num) {
			if (!prime.get(i)) {
				println(i)
			}
		}
	}
	static def main(args : String*) {
		sieve_of_eratosthenes(10)
		sieve_of_eratosthenes(20)
	}
}
</code></pre>

<h2 id="29-exercise-29">29. Exercise 29</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def concat(list : List&lt;String&gt;, num : int) : List&lt;String&gt; {
		var new_list = newArrayList
		for (element : list) {
			for (i : 1..num) {
				new_list += element + i
			}
		}
		return new_list
	}
	static def main(args : String*) {
		println(concat(#['p', 'q'], 5))
	}
}
</code></pre>

<h2 id="30-exercise-30">30. Exercise 30</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def common(list1 : List&lt;Integer&gt;, list2 : List&lt;Integer&gt;) : List&lt;Integer&gt; {
		var common_list = newArrayList
		for (element : list1) {
			if (list2.contains(element)) {
				common_list += element
			}
		}
		return common_list
	}
	static def main(args : String*) {
		println(common(#[1, 2, 4, 6], #[2, 34, 6, 122]))
	}
}
</code></pre>

<h2 id="31-exercise-31">31. Exercise 31</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def change(list : List&lt;Integer&gt;) : List&lt;Integer&gt; {
		var new_list = newArrayList
		for (var i = 0; i &lt; list.size; i += 2) {
			var tmp = list.get(i)
			list.set(i, list.get(i + 1))
			list.set(i + 1, tmp)
		}
		return new_list
	}
	static def main(args : String*) {
		println(change(#[0,1,2,3,4,5]))
	}
}
</code></pre>

<h2 id="32-exercise-32">32. Exercise 32</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def diz(n : int) : int {
		var d = 1
		while (n &gt; d) {
			d *= 10
		}
		return d
	}
	static def merge(list : List&lt;Integer&gt;) : Integer {
		var result = 0
		for (element : list) {
			result *= diz(element)
			result += element
		}
		return result
	}
	static def main(args : String*) {
		println(merge(#[11, 33, 50]))
	}
}
</code></pre>

<h2 id="33-exercise-33">33. Exercise 33</h2>

<pre><code class="language-sarl">import java.util.List
import java.util.Map
class Solution1 {
	static var original = #[
		'be', 'have', 'do', 'say', 'get', 'make',
		'go', 'know', 'take', 'see', 'come', 'think',
		'look', 'want', 'give', 'use', 'find',
		'tell', 'ask', 'work', 'seem', 'feel',
		'leave', 'call']
	static def split_by_letter(words : List&lt;String&gt;) : Map&lt;Character, List&lt;String&gt;&gt; {
		var map : Map&lt;Character, List&lt;String&gt;&gt; = newHashMap
		for (word : words) {
			var letter = word.charAt(0)
			var list = map.computeIfAbsent(letter, [newArrayList])
			list += word
		}
		return map
	} 
	static def main(args : String*) {
		println(split_by_letter(original))
	}
}
class Solution2 {
	static var original = #[
		'be', 'have', 'do', 'say', 'get', 'make',
		'go', 'know', 'take', 'see', 'come', 'think',
		'look', 'want', 'give', 'use', 'find',
		'tell', 'ask', 'work', 'seem', 'feel',
		'leave', 'call']
	static def split_by_letter(words : List&lt;String&gt;) : Map&lt;Character, List&lt;String&gt;&gt; {
		return words.groupBy[it.charAt(0)]
	} 
	static def main(args : String*) {
		println(split_by_letter(original))
	}
}
</code></pre>

<h2 id="34-exercise-34">34. Exercise 34</h2>

<pre><code class="language-sarl">class Solution1 {
	static def main(args : String*) {
		var result = newHashMap
		for (i : 1..10) {
			result.computeIfAbsent(i) [newArrayList]
		}
		println(result)
	}
}
class Solution2 {
	static def main(args : String*) {
		var result = (1..10).toInvertedMap[newArrayList]
		println(result)
	}
}
</code></pre>

<h2 id="35-exercise-35">35. Exercise 35</h2>

<pre><code class="language-sarl">class Solution1 {
	static var original = #[
		#[1, 2], #[3, 4], #[1, 2], #[5, 6], #[7, 8],
		#[1, 2], #[3, 4], #[3, 4], #[7, 8], #[9, 10]
	]
	static def main(args : String*) {
		var result = newArrayList
		for (tuple : original) {
			for (element : tuple) {
				if (!result.contains(element)) {
					result += element
				}
			}
		}
		println(result)
	}
}
class Solution2 {
	static var original = #[
		#[1, 2], #[3, 4], #[1, 2], #[5, 6], #[7, 8],
		#[1, 2], #[3, 4], #[3, 4], #[7, 8], #[9, 10]
	]
	static def main(args : String*) {
		var result = newTreeSet(null)
		for (tuple : original) {
			for (element : tuple) {
				result += element
			}
		}
		println(result)
	}
}
class Solution3 {
	static var original = #[
		#[1, 2], #[3, 4], #[1, 2], #[5, 6], #[7, 8],
		#[1, 2], #[3, 4], #[3, 4], #[7, 8], #[9, 10]
	]
	static def main(args : String*) {
		var result = newTreeSet(null)
		for (tuple : original) {
			result += tuple
		}
		println(result)
	}
}
</code></pre>

<h2 id="36-exercise-36">36. Exercise 36</h2>

<pre><code class="language-sarl">class Solution1 {
	static var original = #[1, 2, 3, 4, 5, 6, 7, 8, 9, 10]
	static def main(args : String*) {
		var result = newArrayList
		for (value : original) {
			if ((value % 2) != 0) {
				result += value
			}
		}
		println(result)
	}
}
class Solution2 {
	static var original = #[1, 2, 3, 4, 5, 6, 7, 8, 9, 10]
	static def main(args : String*) {
		var result = original.filter[(it % 2) != 0].toList
		println(result)
	}
}
</code></pre>

<h2 id="37-exercise-37">37. Exercise 37</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[1, 2, 3, 4, 5, 6, 7, 8, 9, 10]
	static def appendBefore(list : List&lt;Integer&gt;, prefix : Integer) : List&lt;Integer&gt; {
		var result = newArrayList
		for (element : list) {
			result += prefix
			result += element
		}
		return result
	}
	static def main(args : String*) {
		println(appendBefore(original, 34))
	}
}
</code></pre>

<h2 id="38-exercise-38">38. Exercise 38</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[#['assign1', 'assign2'], #['final','assign4'], #['exam','study']]
	static def main(args : String*) {
		for (list : original) {
			if (!list.isEmpty) {
				var iter = list.iterator
				print(iter.next)
				while (iter.hasNext) {
					print(" " + iter.next)
				}
			}
			println
		}
	}
}
</code></pre>

<h2 id="39-exercise-39">39. Exercise 39</h2>

<pre><code class="language-sarl">class Solution {
	static var originalNames = #["Black", "Red", "Maroon", "Yellow"]
	static var originalColors = #["#000000", "#FF0000", "#800000", "#FFFF00"]
	static def main(args : String*) {
		var result = newArrayList
		var iter1 = originalNames.iterator
		var iter2 = originalColors.iterator
		while (iter1.hasNext &amp;&amp; iter2.hasNext) {
			var map = newHashMap
			map.put('color_name', iter1.next)
			map.put('color_code', iter2.next)
			result += map
		}
		println(result)
	}
}
</code></pre>

<h2 id="40-exercise-40">40. Exercise 40</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #['a', 'b', 'c', 'd', 'e', 'f', 'g', 'h', 'i', 'j', 'k', 'l', 'm', 'n']
	static def split_at_n(list : List&lt;String&gt;, n : int) : List&lt;List&lt;String&gt;&gt; {
		var result = newArrayList
		var clist = newArrayList
		for (element : list) {
			if (clist.size == n) {
				result.add(clist)
				clist = newArrayList
			}
			clist += element
		}
		if (!clist.isEmpty) {
			result.add(clist)
		}
		return result
	}
	static def main(args : String*) {
		println(split_at_n(original, 5))
	}
}
</code></pre>

<h2 id="41-exercise-41">41. Exercise 41</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var a = #["red", "orange", "green", "blue", "white"]
	static var b = #["black", "yellow", "green", "blue"]
	static def difference(list1 : List&lt;String&gt;, list2 : List&lt;String&gt;) : List&lt;String&gt; {
		var result = newArrayList
		for (element : list1) {
			if (!list2.contains(element)) {
				result += element
			}
		}
		return result
	}
	static def main(args : String*) {
		println(difference(a, b))
		println(difference(b, a))
	}
}
</code></pre>

<h2 id="42-exercise-42">42. Exercise 42</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def replace_last(list1 : List&lt;Integer&gt;, list2 : List&lt;Integer&gt;) : List&lt;Integer&gt; {
		var result = newArrayList
		result += list1.subList(0, list1.size - 1)
		result += list2
		return result
	}
	static def main(args : String*) {
		println(replace_last(#[1, 3, 5, 7, 9, 10], #[2, 4, 6, 8]))
	}
}
</code></pre>

<h2 id="43-exercise-43">43. Exercise 43</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def exist_in(list : List&lt;Integer&gt;, position : int) : boolean {
		0 &lt;= position &amp;&amp; position &lt; list.size
	}
	static def main(args : String*) {
		println(exist_in(#[1, 3, 5, 7, 9, 10], 3))
		println(exist_in(#[1, 3, 5, 7, 9, 10], 8))
	}
}
</code></pre>

<h2 id="44-exercise-44">44. Exercise 44</h2>

<pre><code class="language-sarl">import java.util.List
class Solution1 {
	static var original = #[1, 2, 3, 4]
	static def append_prefix(list : List&lt;Integer&gt;, prefix : String) : List&lt;String&gt; {
		var result = newArrayList
		for (element : list) {
			result += prefix + element
		}
		return result
	}
	static def main(args : String*) {
		println(append_prefix(original, "emp"))
	}
}
class Solution2 {
	static var original = #[1, 2, 3, 4]
	static def append_prefix(list : List&lt;Integer&gt;, prefix : String) : List&lt;String&gt; {
		list.map[prefix + it].toList
	}
	static def main(args : String*) {
		println(append_prefix(original, "emp"))
	}
}
</code></pre>

<h2 id="45-exercise-45">45. Exercise 45</h2>

<pre><code class="language-sarl">import java.util.List
import java.util.Iterator
class Solution1 {
	static var original1 = #[1, 2, 3]
	static var original2 = #['red', 'white', 'black']
	static def zip(list1 : List&lt;?&gt;, list2 : List&lt;?&gt;) : List&lt;Object[]&gt; {
		var result : List&lt;Object[]&gt; = newArrayList
		val iter1 = list1.iterator
		val iter2 = list2.iterator
		while (iter1.hasNext &amp;&amp; iter2.hasNext) {
			result.add(#[iter1.next, iter2.next])
		}
		return result
	}
	static def main(args : String*) {
		for (tuple : zip(original1, original2)) {
			println(tuple)
		}
	}
}
class Solution2 {
	static var original1 = #[1, 2, 3]
	static var original2 = #['red', 'white', 'black']
	static def zip(list1 : List&lt;?&gt;, list2 : List&lt;?&gt;) : Iterable&lt;Object[]&gt; {
		[
			val iter1 = list1.iterator
			val iter2 = list2.iterator
			new Iterator&lt;Object[]&gt; {
				def hasNext : boolean {
					iter1.hasNext &amp;&amp; iter2.hasNext
				}
				def next : Object[] {
					var v1 = iter1.next
					var v2 = iter2.next
					return #[v1, v2]
				}
			}
		]
	}
	static def main(args : String*) {
		for (tuple : zip(original1, original2)) {
			println(tuple)
		}
	}
}
</code></pre>

<h2 id="46-exercise-46">46. Exercise 46</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[0, 10, #[20, 30], 40, 50, #[60, 70, 80], #[90, 100, 110, 120]]
	static def flatten_list(list : List&lt;?&gt;) : List&lt;?&gt; {
		var result = newArrayList
		for (element : list) {
			if (element instanceof Iterable) {
				result.addAll(element)
			} else {
				result += element
			}
		}
		return result
	}
	static def main(args : String*) {
		println(flatten_list(original))
	}
}
</code></pre>

<h2 id="47-exercise-47">47. Exercise 47</h2>

<pre><code class="language-sarl">class Solution {
	static var original = #[0, 0, 1, 2, 3, 4, 4, 5, 6, 6, 6, 7, 8, 9, 4, 4]
	static def main(args : String*) {
		var result = newArrayList
		for (element : original) {
			if (result.isEmpty || element != result.get(result.size - 1)) {
				result += element
			}
		}
		println(result)
	}
}
</code></pre>

<h2 id="48-exercise-48">48. Exercise 48</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[0, 0, 1, 2, 3, 4, 4, 5, 6, 6, 6, 7, 8, 9, 4, 4]
	static def main(args : String*) {
		var result = newArrayList
		var pack : List&lt;Integer&gt; = null
		var prev : Integer = null
		for (element : original) {
			if (element != prev) {
				if (!pack.isNullOrEmpty) {
					result.add(pack)
				}
				pack = newArrayList
			}
			pack += element
			prev = element
		}
		if (!pack.isNullOrEmpty) {
			result.add(pack)
		}
		println(result)
	}
}
</code></pre>

<h2 id="49-exercise-49">49. Exercise 49</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[1, 1, 2, 3, 4, 4, 5, 1]
	static def insert_at(list : List&lt;Integer&gt;, position : int, element : Integer) : List&lt;Integer&gt; {
		var new_list = newArrayList
		new_list.addAll(list)
		if (position &gt;= new_list.size) {
			new_list += element
		} else {
			new_list.add(position, element)
		}
		return new_list
	}
	static def main(args : String*) {
		println(insert_at(original, 3, 12))
	}
}
</code></pre>

<h2 id="50-exercise-50">50. Exercise 50</h2>

<pre><code class="language-sarl">import java.util.List
import java.util.Random
class Solution {
	static var original = #[1, 1, 2, 3, 4, 4, 5, 1]
	static def extract_random(list : List&lt;Integer&gt;, n : int) : List&lt;Integer&gt; {
		var new_list = newArrayList
		if (n &lt;= list.size) {
			new_list.addAll(list)
		} else {
			var random = new Random
			var selected = newHashSet
			for (i : 1..n) {
				var rnd = random.nextInt(list.size)
				while (!selected.add(rnd)) {
					rnd = random.nextInt(list.size)
				}
				new_list.add(list.get(rnd))
			}
		}
		return new_list
	}
	static def main(args : String*) {
		println(extract_random(original, 3))
	}
}
</code></pre>

<h2 id="51-exercise-51">51. Exercise 51</h2>

<pre><code class="language-sarl">class Solution {
	static var original = #[1, 2, 3, 4, 5, 6, 7, 8, 9]
	static def main(args : String*) {
		var result = newArrayList
		for (var i = 0; i &lt; original.size - 1; i++) {
			var elt0 = original.get(i)
			for (var j = i + 1; j &lt; original.size; j++) {
				var elt1 = original.get(j)
				result.add(#[elt0, elt1])
			}
		}
		println(result)
	}
}
</code></pre>

<h2 id="52-exercise-52">52. Exercise 52</h2>

<pre><code class="language-sarl">class Solution1 {
	static var original = #[22.4, 4.0, -16.22, -9.1, 11.0, -12.22, 14.2, -5.2, 17.5]
	static def main(args : String*) {
		var sum = 0l
		for (element : original) {
			sum += Math::round(element)
		}
		println(sum * original.size)
	}
}
class Solution2 {
	static var original = #[22.4, 4.0, -16.22, -9.1, 11.0, -12.22, 14.2, -5.2, 17.5]
	static def main(args : String*) {
		var result = original.size * original.map[Math::floor(it)].reduce[accumulator, current| accumulator + current]
		println(result)
	}
}
</code></pre>

<h2 id="53-exercise-53">53. Exercise 53</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def create_multidimensional_list(a : int, b : int) : List&lt;List&lt;Integer&gt;&gt; {
		var list = newArrayList
		for (i : 1..a) {
			var sublist = newArrayList
			for (j : 1..b) {
				sublist += 0
			}
			list.add(sublist)
		}
		return list
	}
	static def main(args : String*) {
		println(create_multidimensional_list(3, 2))
	}
}
</code></pre>

<h2 id="54-exercise-54">54. Exercise 54</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static def input_matrix(args : String[]) : List&lt;List&lt;Integer&gt;&gt; {
		var n = args.get(0) as int
		var m = newArrayList
		var k = 1
		for (i : 1..n) {
			var row = newArrayList
			for (j : 1..n) {
				row.add(args.get(k) as int)
				k++
			}
			m.add(row)
		}
		return m
	}
	static def sum_diag(matrix : List&lt;List&lt;Integer&gt;&gt;) : int {
		var sum = 0
		for (i : 0..&lt;matrix.size) {
			sum += matrix.get(i).get(i)
		}
		return sum
	}
	static def main(args : String*) {
		var matrix = input_matrix(args)
		var result = sum_diag(matrix)
		println(result)
	}
}
</code></pre>

<h2 id="55-exercise-55">55. Exercise 55</h2>

<pre><code class="language-sarl">import java.util.List
import java.util.Iterator
class Solution {
	static var original1 = #[#[1, 3], #[5, 7], #[9, 11]]
	static var original2 = #[#[2, 4], #[6, 8], #[10, 12, 14]]
	static def zip(list1 : List&lt;List&lt;Integer&gt;&gt;, list2 : List&lt;List&lt;Integer&gt;&gt;) : List&lt;List&lt;Integer&gt;&gt; {
		var iter1 = list1.iterator
		var iter2 = list2.iterator
		var result = newArrayList
		while (iter1.hasNext &amp;&amp; iter2.hasNext) {
			var sublist = newArrayList
			sublist.addAll(iter1.next)
			sublist.addAll(iter2.next)
			result.add(sublist)
		}
		if (iter1.hasNext) {
			do {
				result.add(iter1.next)
			} while (iter1.hasNext)
		} else if (iter2.hasNext) {
			do {
				result.add(iter2.next)
			} while (iter2.hasNext)
		}
		return result
	}
	static def main(args : String*) {
		var result = zip(original1, original2)
		println(result)
	}
}
</code></pre>

<h2 id="56-exercise-56">56. Exercise 56</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[1, 1, 3, 4, 4, 5, 6, 7]
	static def isseq(list : List&lt;Integer&gt;, i : int, n : int) : boolean {
		var elt = list.get(i)
		for (var j = i + 1; j &lt; n; j++) {
			if (list.get(j) != elt) {
				return false
			}
		}
		return true
	}
	static def extract(list : List&lt;Integer&gt;, n : int) : List&lt;Integer&gt; {
		var result = newArrayList
		for (var i = 0; i &lt;= list.size - n; i++) {
			if (isseq(list, i, n)) {
				result += list.get(i)
			}
		}
		return result
	}
	static def main(args : String*) {
		var result = extract(original, 2)
		println(result)
	}
}
</code></pre>

<h2 id="57-exercise-57">57. Exercise 57</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original1 = #[1, 1, 3, 4, 4, 5, 6, 7]
	static var original2 = #[0, 1, 2, 3, 4, 4, 5, 7, 8]
	static def average(lists : List&lt;Integer&gt;*) : double {
		var sum = 0.0
		var count = 0
		for (list : lists) {
			count += list.size
			for (elt : list) {
				sum += elt
			}
		}
		return sum / count
	}
	static def main(args : String*) {
		var result = average(original1, original2)
		println(result)
	}
}
</code></pre>

<h2 id="58-exercise-58">58. Exercise 58</h2>

<pre><code class="language-sarl">import java.util.List
import java.math.BigInteger
class Solution1 {
	static var original = #[1, 'abcd', 3, 1.2, 4, 'xyz', 5, 'pqr', 7, -5, -12.22]
	static def isInteger(value : Object) : boolean {
		value instanceof Integer || value instanceof Long || value instanceof BigInteger
	}
	static def count_integers(list : List&lt;?&gt;) : int {
		var count = 0
		for (element : list) {
			if (element.isInteger) {
				count++
			}
		}
		return count
	}
	static def main(args : String*) {
		println(count_integers(original))
	}
}
class Solution2 {
	static var original = #[1, 'abcd', 3, 1.2, 4, 'xyz', 5, 'pqr', 7, -5, -12.22]
	static def count_integers(list : List&lt;?&gt;) : int {
		list.filter[it instanceof Integer || it instanceof Long || it instanceof BigInteger].size
	}
	static def main(args : String*) {
		println(count_integers(original))
	}
}
</code></pre>

<h2 id="59-exercise-59">59. Exercise 59</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[#[1, 2, 3], #[2, 4, 5], #[1, 1, 1]]
	static def remove_column(list : List&lt;List&lt;Integer&gt;&gt;, col : int) : List&lt;List&lt;Integer&gt;&gt; {
		var rows = newArrayList
		for (row : list) {
			var ncols = newArrayList
			ncols.addAll(row)
			ncols.remove(col)
			rows.add(ncols)
		}
		return rows
	}
	static def main(args : String*) {
		println(remove_column(original, 1))
	}
}
</code></pre>

<h2 id="60-exercise-60">60. Exercise 60</h2>

<pre><code class="language-sarl">import java.util.List
class Solution {
	static var original = #[#[1, 2, 3], #[2, 4, 5], #[1, 1, 1]]
	static def get_column(list : List&lt;List&lt;Integer&gt;&gt;, col : int) : List&lt;Integer&gt; {
		var column = newArrayList
		for (row : list) {
			column += row.get(col)
		}
		return column
	}
	static def main(args : String*) {
		println(get_column(original, 1))
	}
}
</code></pre>

<h2 id="61-legal-notice">61. Legal Notice</h2>

<ul>
  <li>Specification: SARL General-purpose Agent-Oriented Programming Language (“Specification”)</li>
  <li>Version: 0.13</li>
  <li>Status: Stable Release</li>
  <li>Release: 2023-09-19</li>
</ul>

<blockquote>
  <p>Copyright © 2014-2023 <a href="https://www.sarl.io/about/index.html">SARL.io, the Original Authors and Main Authors</a>.</p>

  <p>Documentation text and medias are licensed under the Creative Common CC-BY-SA-4.0;
you may not use this file except in compliance with CC-BY-SA-4.0.
You may obtain a copy of <a href="https://creativecommons.org/licenses/by-sa/4.0/deed.en">CC-BY-4.0</a>.</p>

  <p>Examples of SARL code are licensed under the Apache License, Version 2.0;
you may not use this file except in compliance with the Apache License.
You may obtain a copy of the <a href="http://www.apache.org/licenses/LICENSE-2.0">Apache License</a>.</p>

  <p>You are free to reproduce the content of this page on copyleft websites such as Wikipedia.</p>
</blockquote>

<p><small>Generated with the translator docs.generator 0.13.0.</small></p>

    </div> <!-- /default layout container -->

    <div class="container">
  <!-- FOOTER -->
  <footer>
    <div class="row">
      
    <div class="col-lg-3">
    <div class="well well-sm">
      
      <center>
      <h2>SARL</h2>
      <div>Agent-Oriented Programming Language</div>
      <img src="../../../images/SARL-logo.png" style="width:30%;max-width:64px;"/><br/>
      <small><a href="../../../images/SARL-logo.png">png</a>&nbsp;&middot;&nbsp;<a href="../../../images/SARL-logo.svg">svg</a></small>
      </center>
    </div>
    </div>
    <div class="col-lg-3">
      <div class="well well-sm">
      <h2><i class="glyphicon glyphicon-th-large"></i> Quick Links </h2> 
      <ul class="fa-ul">
		  <li>
			<a href="../../../search/index.html">
			  <i class="fa-li fa fa-search"></i> Search
			</a>
		  </li>
		  <li>
			<a href="../../../news/index.html">
			  <i class="fa-li fa fa-feed"></i> News
			</a>
		  </li>
		  <li>
			<a href="../../../about/index.html">
			  <i class="fa-li fa fa-info"></i> About
			</a>
		  </li>
		  <li>
			<a href="../../../about/index.html#license">
			  <i class="fa-li fa fa-legal"></i> License
			</a>
		  </li>
		  <li>
			<a href="../../../supports/index.html">
			  <i class="fa-li fa fa-asterisk"></i> Supports&amp;Sponsors
			</a>
		  </li>
      </ul>
      </div> 
    </div>
    <div class="col-lg-3">
      <div class="well well-sm">
      <h2><i class="glyphicon glyphicon-cog"></i> Support</h2>
      <ul class="fa-ul">
		  <li>
			<a href="../../../community/forums.html">
			  <i class="fa-li fa fa-comments"></i> User Forums
			</a>
		  </li>
		
		  <li>
			<a href="https://gitter.im/sarl/Lobby" target="_blank">
			  <i class="fa-li fa fa-wechat"></i> Chat Room
			</a>
		  </li>
		
		
		  <li>
			<a href="https://github.com/sarl/sarl" target="_blank">
			  <i class="fa-li fa fa-github"></i> GitHub
			</a>
		  </li>
		  <li>
			<a href="https://github.com/sarl/sarl/issues" target="_blank">
			  <i class="fa-li fa fa-bug"></i> Report an Issue
			</a>
		  </li>
		
		
		
		  <li>
			<a href="https://www.facebook.com/sarllanguage" target="_blank">
			  <i class="fa-li fa fa-facebook"></i> Facebook
			</a>
		  </li>
		
		
		
      </ul>
      </div> 
    </div>
    <div class="col-lg-3">
      <div class="well well-sm">
      <h2><i class="glyphicon glyphicon-link"></i> Links</h2>
      <ul class="fa-ul">
        <li><a href="http://www.ciad-lab.fr" target="_blank"><i class="fa-li fa fa-university"></i>MAS Group - CIAD - UBFC</a></li>
        <li><a href="https://www.rmit.edu.au/contact/staff-contacts/academic-staff/r/rodriguez-dr-sebastian" target="_blank"><i class="fa-li fa fa-university"></i>RMIT</a></li>
        <li><a href="http://www.janusproject.io" target="_blank"><i class="fa-li fa fa-gears"></i>Janus Project</a></li>
      </ul>
      </div> 
    </div>
    </div>
    
  </footer>
</div>


    <!-- Bootstrap core JavaScript
    ================================================== -->
    <!-- Placed at the end of the document so the pages load faster -->
    <script src="../../../js/jquery.min.js"></script>
    <script src="../../../js/bootstrap.min.js"></script>
    <script src="../../../js/run_prettify.js"></script>
	<script src="../../../js/lang-sarl.js"></script>


	<!-- Google analystics -->
	<script>
	  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
	  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
	  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
	  })(window,document,'script','https://www.google-analytics.com/analytics.js','ga');
	  ga('create', 'UA-106498978-1', 'auto');
	  ga('send', 'pageview');
	</script>

  </body>
</html>
